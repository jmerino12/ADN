plugins {
    id 'com.android.application'
    id 'kotlin-android'
    id 'kotlin-kapt'
}
android {
    compileSdk 31
    /*packagingOptions {
        pickFirst 'META-INF/AL2.0'
        pickFirst 'META-INF/LGPL2.1'
        pickFirst("win32-x86/attach_hotspot_windows.dll")
        pickFirst("win32-x86-64/attach_hotspot_windows.dll")
        pickFirst("META-INF/licenses/ASM")
    }*/
    defaultConfig {
        applicationId "com.example.adn"
        minSdk 21
        targetSdk 31
        versionCode 1
        versionName "1.0"

        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }

        debug{
            testCoverageEnabled true
        }
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
    kotlinOptions {
        jvmTarget = '1.8'
    }
    buildFeatures {
        viewBinding true
    }
}

jacoco {
    toolVersion = "$jacoco_version"
}


task jacocoTestReport(type: JacocoReport, dependsOn: [':domain:test','connectedAndroidTest']) {
    reports {
        xml.enabled  true
        html.enabled  true
    }

    def domainProject = project(':domain')

    def kotlinDebugTree = layout.files("$buildDir/tmp/kotlin-classes/debug","$domainProject.buildDir/classes/koltin")
    def mainSource = layout.files("$project.projectDir/src/main/java","$domainProject.projectDir/src/main/java")
    def executionFilesFromConnectedAndroidTest = fileTree(
            dir: "$buildDir",
            includes: ["outputs/code_coverage/debugAndroidTest/connected/**coverage.ec"]
    )


    def executionFileForUnitTestInModule = "$buildDir/outputs/unit_test_code_coverage/debugUnitTest/testDebugUnitTest.exec"

    def executionFileForUnitTestInOtherModule = "$domainProject.buildDir/jacoco/test.exec"

    def executionFiles = layout.files(
            executionFilesFromConnectedAndroidTest,
            executionFileForUnitTestInModule,
            executionFileForUnitTestInOtherModule
    )
    sourceDirectories.setFrom(files([mainSource]))
    classDirectories.setFrom(files([kotlinDebugTree.asFileTree]))
    executionData.setFrom(executionFiles.asFileTree)

}

subprojects {
    configurations.all {
        resolutionStrategy {
            eachDependency { details ->
                if ('org.jacoco' == details.requested.group) {
                    details.useVersion jacoco_version
                }
            }
        }
    }
}


dependencies {
    implementation project(":data")
    implementation project(":domain")
    implementation project(":usecases")
    implementation androidLibs.values()
    kapt androidKaptLibs.values()
    testImplementation testLibs.values()
    androidTestImplementation androidTestLibs.values()

}